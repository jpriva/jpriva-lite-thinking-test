import org.springframework.boot.gradle.tasks.aot.ProcessTestAot

plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.hibernate.orm' version '7.2.0.Final'
	id 'org.graalvm.buildtools.native' version '0.11.3'
}

group = 'com.jpriva'
version = '0.0.1-SNAPSHOT'
description = 'Lite Thinking Test- Juan Pablo Rivadeneira'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom "software.amazon.awssdk:bom:2.41.10"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.flywaydb:flyway-sqlserver'
	implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
	implementation 'com.itextpdf:kernel:9.5.0'
	implementation 'com.itextpdf:layout:9.5.0'
	implementation 'com.itextpdf:io:9.5.0'
	//AWS
	implementation platform('software.amazon.awssdk:bom:2.41.10')
	implementation 'software.amazon.awssdk:s3'
	implementation 'software.amazon.awssdk:sqs'
	// OPENAPI
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'

	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
	implementation 'com.microsoft.sqlserver:mssql-jdbc'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-flyway-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-validation-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
	testImplementation 'org.testcontainers:testcontainers-mssqlserver'
	testImplementation 'org.testcontainers:testcontainers-localstack'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

hibernate {
	enhancement {
		enableAssociationManagement = true
	}
}

test {
	useJUnitPlatform()
	jvmArgs("-agentlib:native-image-agent=config-output-dir=src/main/resources/META-INF/native-image")
	jvmArgs("-XX:+EnableDynamicAgentLoading")
	testLogging {
		events "failed", "skipped"

		exceptionFormat = "full"

		showStandardStreams = true
	}

	afterSuite { desc, result ->
		if (!desc.parent) {
			println "\n=========================================================="
			println "RESULTADOS DEL TEST: ${result.resultType}"
			println "Total: ${result.testCount}"
			println "Pasaron: ${result.successfulTestCount} (✅)"
			println "Fallaron: ${result.failedTestCount} (❌)"
			println "Saltados: ${result.skippedTestCount} (⚠️)"
			println "==========================================================\n"
		}
	}
}

//tasks.withType(ProcessTestAot).configureEach {
//	enabled = false
//}

graalvmNative {
	binaries {
		main {
			buildArgs.add("-H:IncludeResources=db/migration/.*")
			buildArgs.add("-H:+AddAllCharsets")
			buildArgs.add("-H:IncludeResources=.*\\.ttf\$")
			buildArgs.add("-H:IncludeResources=.*\\.otf\$")
			resources.autodetect()
			buildArgs.add("--initialize-at-build-time=net.bytebuddy")
            buildArgs.add("--initialize-at-build-time=org.hibernate.bytecode.internal.bytebuddy")
            buildArgs.add("--initialize-at-build-time=org.hibernate.bytecode.spi.BytecodeProvider")
		}
	}
}